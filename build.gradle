
plugins {
    id 'java'
    id 'maven-publish'
    id 'signing'
}

repositories {
    mavenCentral()
}

version = '1.3.0'

dependencies {
    // JUnit Tests
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.assertj:assertj-core:3.8.0'
    testImplementation 'org.mockito:mockito-core:3.+'

    implementation 'org.apache.commons:commons-lang3:3.8.1'
    implementation 'org.usb4java:libusb4java:1.3.0'
    implementation 'org.usb4java:usb4java-javax:1.3.0'
}

//Exclude all tests by default
test {
    exclude '**/*'
}

task myJavadocs(type: Javadoc) {
    source = sourceSets.main.allJava
}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    repositories {
        maven {
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username = ossrhUsername
                password = ossrhPassword
            }
        }
    }
    publications {
        maven(MavenPublication) {
            groupId "io.github.dsheirer"
            artifactId "usb4java"
            from components.java
//            version "1.3.0.1"
//            artifact ("jars/usb4java-1.3.0.jar")
            pom {
                name = 'usb4java 1.3.0 modified with system property library loading'
                description = 'usb4java modified library'
                url = 'https://github.com/DSheirer/usb4java'
                licenses {
                    license {
                        name = 'GNU General Public License, Version 3.0'
                        url = 'https://www.gnu.org/licenses/gpl-3.0.en.html'
                    }
                }

                developers {
                    developer {
                        id = 'dsheirer'
                        name = 'Dennis Sheirer'
                        email = 'dsheirer@sonatype.com'
                        organizationUrl = 'https://github.com/DSheirer'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/DSheirer/usb4java.git'
                    developerConnection = 'scm:git:ssh://github.com/DSheirer/usb4java.git'
                    url = 'https://github.com/DSheirer/usb4java/tree/master'
                }
            }
        }
    }
}

signing {
    sign publishing.publications
}